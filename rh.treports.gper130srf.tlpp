#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "TREPORTS_GPER130.ch"

namespace totvs.protheus.rh.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAGPE", tables="CTT, SRA, SRF", name="Aviso de férias a partir de Programação de Férias", country="ALL", initialRelease="12.1.2210")
class GPER130SRFTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass
 
method new() as object class GPER130SRFTReportsBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Aviso de férias a partir de Programação de Férias"
return self
 
method getDescription() as character class GPER130SRFTReportsBusinessObject
return STR0003//"Objeto contendo informações do aviso de férias a partir de Programação de Férias"
 
method getAreas() as array class GPER130SRFTReportsBusinessObject
return {STR0002}//"RH"
 
method getData(nPage as numeric, oFilter as object) as object class GPER130SRFTReportsBusinessObject
    local aInfo as array
    local cAlias as character
    local cDescCTT as character
    local cQuery as character
    local cUltFil as character
    local dDtAviso as date
    local dDtFim as date
    local dDtIni as date
    local dDtPgto as date
    local dDtRetorno as date
    local lHasNext as logical
    local nDiasFer as numeric
    local nStart as numeric
    local nEnd as numeric
 
    lHasNext := .F.
    
    self:setPageSize(100)

    //Seta o primeiro item e o último da página atual
    nStart := ((nPage - 1) * self:getPageSize()) + 1
    nEnd   := nPage * self:getPageSize() + 1

    cQuery  := "SELECT * FROM ("
    cQuery  +=      "SELECT ROW_NUMBER() OVER(ORDER BY SRF.RF_FILIAL, SRF.RF_MAT, SRF.RF_DATABAS, SRF.RF_DATAINI, SRF.RF_DATINI2, SRF.RF_DATINI3 ASC) AS LINHA, SRF.RF_FILIAL, SRF.RF_MAT, SRF.RF_DATABAS, SRF.RF_DATAFIM, SRF.RF_DATAINI, SRF.RF_DATINI2, SRF.RF_DATINI3, SRF.RF_DFEPRO1, SRF.RF_DFEPRO2, SRF.RF_DFEPRO3, SRA.RA_CC, SRA.RA_NOME, SRA.RA_NOMECMP, SRA.RA_NUMCP, SRA.RA_SERCP FROM " + RetSqlName("SRF") + " SRF "
    cQuery  +=      "INNER JOIN " + RetSqlName("SRA") + " SRA ON SRA.RA_FILIAL = SRF.RF_FILIAL AND SRA.RA_MAT = SRF.RF_MAT AND SRA.D_E_L_E_T_ = ' ' "
    cQuery  +=      "WHERE SRF.RF_STATUS = '1' AND "
    cQuery  +=      "( SRF.RF_DATAINI != '        ' OR SRF.RF_DATINI2 != '        ' OR SRF.RF_DATINI3 != '        ') AND "
    cQuery  +=      "SRF.D_E_L_E_T_ = ' '"
    //Os filtros serão setados na interface do novo TReports
    if oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    endif
    cQuery  += ") AS TAB "
    cQuery  += "WHERE LINHA BETWEEN '" + cValToChar(nStart) + "' AND '" + cValToChar(nEnd) + "'"

    cAlias := MPSysOpenQuery(cQuery)

    while !(cAlias)->(Eof())
        If (cAlias)->LINHA > (nEnd - 1)
            lHasNext := .T.
            Exit
        EndIf

        cDescCTT    := DescCc( (cAlias)->RA_CC, (cAlias)->RF_FILIAL )
        dDtIni      := Iif( !Empty((cAlias)->RF_DATINI3), sToD( (cAlias)->RF_DATINI3 ), Iif( !Empty((cAlias)->RF_DATINI2), sToD( (cAlias)->RF_DATINI2 ), sToD( (cAlias)->RF_DATAINI ) ) )
        dDtAviso    := dDtIni - 30
        nDiasFer    := Iif( !Empty((cAlias)->RF_DFEPRO3), (cAlias)->RF_DFEPRO3, Iif( !Empty((cAlias)->RF_DFEPRO2), (cAlias)->RF_DFEPRO2, (cAlias)->RF_DFEPRO1) )
        dDtFim      := dDtIni + nDiasFer - 1
        dDtRetorno  := dDtFim + 1
        dDtPgto     := Iif( !Empty(dDtIni), DataValida( DataValida( dDtIni-1, .F.)-1, .F.), cToD("//") )

        If cUltFil != (cAlias)->RF_FILIAL
            cUltFil := (cAlias)->RF_FILIAL
            fInfo(@aInfo, (cAlias)->RF_FILIAL)
        EndIf

        self:oData:appendData({"RF_FILIAL": (cAlias)->RF_FILIAL,;
                "RF_MAT": (cAlias)->RF_MAT,;
                "RF_DATABAS": FwTimeStamp( 5, sToD( (cAlias)->RF_DATABAS ), "00:00:00" ),;
                "RF_DATAFIM": FwTimeStamp( 5, sToD( (cAlias)->RF_DATAFIM ), "00:00:00" ),;
                "RF_DATAINI": FwTimeStamp( 5, dDtIni, "00:00:00" ),;
                "FIM": FwTimeStamp( 5, dDtFim, "00:00:00" ),;
                "AVISO": FwTimeStamp( 5, dDtAviso, "00:00:00" ),;
                "RECIBO": FwTimeStamp( 5, dDtPgto, "00:00:00" ),;
                "RETORNO": FwTimeStamp( 5, dDtRetorno, "00:00:00" ),;
                "FILCID": aInfo[5],;
                "FILNOME": aInfo[3],;
                "RF_DFEPRO1": nDiasFer,;
                "RA_CC": (cAlias)->RA_CC,;
                "CTT_DESC01": cDescCTT,;
                "RA_NOME": (cAlias)->RA_NOME,;
                "RA_NOMECMP": (cAlias)->RA_NOMECMP,;
                "RA_NUMCP": (cAlias)->RA_NUMCP,;
                "RA_SERCP": (cAlias)->RA_SERCP })
        (cAlias)->( dbSkip() )
    enddo

//Se não for o último registro indica que terá próxima página
self:setHasNext( lHasNext )
 
(cAlias)->( DBCloseArea() )

return self:oData
 
method getSchema() as object class GPER130SRFTReportsBusinessObject
    local aFieldsSRA as array
    local aFieldsSRF as array
    
    aFieldsSRA := { "RA_CC", "RA_NOME", "RA_NOMECMP", "RA_NUMCP", "RA_SERCP" }
    aFieldsSRF := { "RF_FILIAL", "RF_MAT", "RF_DATABAS", "RF_DATAFIM", "RF_DATAINI", "RF_DFEPRO1" }
    
    self:oSchema:aliasToSchema("CTT", "CTT_DESC01")
    self:oSchema:aliasToSchema("SRA", aFieldsSRA)
    self:oSchema:aliasToSchema("SRF", aFieldsSRF)
    self:oSchema:addProperty("FIM", "Fim das Ferias", "date", "Fim Férias", "FIM")
    self:oSchema:addProperty("AVISO", "Data de Aviso de Ferias  ", "date", "Dt.Av.Ferias", "AVISO")
    self:oSchema:addProperty("RECIBO", "Data de Recibo das Ferias", "date", "Dt.Rec.Fer.", "RECIBO")
    self:oSchema:addProperty("RETORNO", "Data do retorno", "date", "Dt.Retorno", "RETORNO")
    self:oSchema:addProperty("FILCID", "Cidade Filial", "string", "Cidade Fil.", "FILCID")
    self:oSchema:addProperty("FILNOME", "Nome Filial", "string", "Nome Filial", "FILNOME")
return self:oSchema
